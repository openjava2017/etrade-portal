
ext {
    springVersion = '4.3.13.RELEASE'
    mybatisVersion = '3.4.5'
    mybatisSpringVersion = '1.3.1'
    druidVersion = '1.1.6'
    mysqlConnectorVersion = '6.0.6'
    jedisVersion = '2.9.0'
    quartzVersion = '2.3.0'
    buildVersion = '1.0.0'
}

task release(dependsOn: 'build') << {
}

def loadGroovyConfig() {
    def profile = project['profile']
    def configFile = file('config.groovy')
    new ConfigSlurper(profile).parse(configFile.toURI().toURL()).toProperties()
}

gradle.taskGraph.whenReady {taskGraph ->
    if (taskGraph.hasTask(release)) {
        version = buildVersion
    } else {
        version = buildVersion + '-SNAPSHOT'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'idea'
    apply plugin: 'eclipse-wtp'

    version = buildVersion
    sourceCompatibility = 1.8

    compileJava {
        options.encoding = 'UTF-8'
    }

    processResources {
        from(sourceSets.main.resources.srcDirs) {
            filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: loadGroovyConfig())
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "http://mvn3.diligrp.com"
        }
    }

    dependencies {
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
        compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
        testCompile group: 'junit', name: 'junit', version: '4.11'
    }

    jar {
        manifest {
            attributes 'Implementation-Title': 'Dili Group RDC',
                       'Implementation-Version': version
        }
    }
}